<!DOCTYPE html>
<html lang="en">
<head>
    <meta charset="utf-8">
    <title>src/signaling.coffee</title>
    <link rel="stylesheet" href="http://yui.yahooapis.com/3.9.1/build/cssgrids/cssgrids-min.css">
    <link rel="stylesheet" href="../assets/vendor/prettify/prettify-min.css">
    <link rel="stylesheet" href="../assets/css/main.css" id="site_styles">
    <link rel="icon" href="../assets/favicon.ico">
    <script src="http://yui.yahooapis.com/combo?3.9.1/build/yui/yui-min.js"></script>
</head>
<body class="yui3-skin-sam">

<div id="doc">
    <div id="hd" class="yui3-g header">
        <div class="yui3-u-3-4">
                <h1><img src="../assets/css/logo.png" title="" width="117" height="52"></h1>
        </div>
        <div class="yui3-u-1-4 version">
            <em>API Docs for: </em>
        </div>
    </div>
    <div id="bd" class="yui3-g">

        <div class="yui3-u-1-4">
            <div id="docs-sidebar" class="sidebar apidocs">
                <div id="api-list">
                    <h2 class="off-left">APIs</h2>
                    <div id="api-tabview" class="tabview">
                        <ul class="tabs">
                            <li><a href="#api-classes">Classes</a></li>
                            <li><a href="#api-modules">Modules</a></li>
                        </ul>
                
                        <div id="api-tabview-filter">
                            <input type="search" id="api-filter" placeholder="Type to filter APIs">
                        </div>
                
                        <div id="api-tabview-panel">
                            <ul id="api-classes" class="apis classes">
                                <li><a href="../classes/Channel.html">Channel</a></li>
                                <li><a href="../classes/Guest.html">Guest</a></li>
                                <li><a href="../classes/Hotel.html">Hotel</a></li>
                                <li><a href="../classes/Room.html">Room</a></li>
                                <li><a href="../classes/WebsocketChannel.html">WebsocketChannel</a></li>
                            </ul>
                
                
                            <ul id="api-modules" class="apis modules">
                                <li><a href="../modules/easy-signaling.html">easy-signaling</a></li>
                            </ul>
                        </div>
                    </div>
                </div>
            </div>
        </div>
        <div class="yui3-u-3-4">
                <div id="api-options">
                    Show:
                    <label for="api-show-inherited">
                        <input type="checkbox" id="api-show-inherited" checked>
                        Inherited
                    </label>
            
                    <label for="api-show-protected">
                        <input type="checkbox" id="api-show-protected">
                        Protected
                    </label>
            
                    <label for="api-show-private">
                        <input type="checkbox" id="api-show-private">
                        Private
                    </label>
                    <label for="api-show-deprecated">
                        <input type="checkbox" id="api-show-deprecated">
                        Deprecated
                    </label>
            
                </div>
            
            <div class="apidocs">
                <div id="docs-main">
                    <div class="content">
<h1 class="file-heading">File: src/signaling.coffee</h1>

<div class="file">
    <pre class="code prettyprint linenums">
###############################################################################
#
#  easy-signaling - A WebRTC signaling server
#  Copyright (C) 2014  Stephan Thamm
#
#  This program is free software: you can redistribute it and/or modify
#  it under the terms of the GNU Affero General Public License as
#  published by the Free Software Foundation, either version 3 of the
#  License, or (at your option) any later version.
#
#  This program is distributed in the hope that it will be useful,
#  but WITHOUT ANY WARRANTY; without even the implied warranty of
#  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#  GNU Affero General Public License for more details.
#
#  You should have received a copy of the GNU Affero General Public License
#  along with this program.  If not, see &lt;http://www.gnu.org/licenses/&gt;.
#
###############################################################################

###*
# Concept of a channel connecting the client to the signaling server. This is not an actual class but the description of the interface used to represent the communication channels. For a reference implementation look at &#x60;WebsocketChannel&#x60;.
#
# The interface expects JavaScript Objects to come in and out of the API. You most propably want to encode the messages on the transport channel, for example using JSON.
#
# @class Channel
# @extends events.EventEmitter
###
###*
# A message was received. You might have to decode the data.
# @event message
# @param {Object} data The decoded message
###
###*
# The connection was closed
# @event closed
###
###*
# An error occured with the underlying connection.
# @event error
# @param {Error} error The error which occured
###
###*
# Send data to the client. You might have to encode the data for transmission.
# @method send
# @param {Object} data The message to be sent
###
###*
# Close the connection to the client
# @method close
###

uuid = require(&#x27;node-uuid&#x27;)

EventEmitter = require(&#x27;events&#x27;).EventEmitter

is_empty = (obj) -&gt;
  for _, _ of obj
    return false

  return true

###*
# A simple signaling server for WebRTC applications
# @module easy-signaling
###

###*
# Manages &#x60;Room&#x60;s and its &#x60;Guest&#x60;s
# @class Hotel
# @extends events.EventEmitter
#
# @constructor
#
# @example
#     var hotel = new Hotel()
#     guest_a = hotel.create_guest(conn_a, &#x27;room&#x27;)
#     guest_b = hotel.create_guest(conn_b, &#x27;room&#x27;)
###
class Hotel extends EventEmitter

  ###*
  # A new room was created
  # @event room_created
  # @param {Room} room The new room
  ###

  ###*
  # A new room was removed because all guests left
  # @event room_removed
  # @param {Room} room The empty room
  ###

  ###*
  # An object containing the rooms with their names as keys
  # @property rooms
  # @private
  ###

  constructor: () -&gt;
    @rooms = {}


  ###*
  # Get a room. The room is created if it did not exist. The room will be removed when it throws &#x60;empty&#x60;.
  # @method get_room
  # @private
  # @param {String} name The name of the room
  # @return {Room}
  ###
  get_room: (name) -&gt;
    if @rooms[name]?
      return @rooms[name]

    room = @rooms[name] = new Room(name, this)

    room.on &#x27;empty&#x27;, () =&gt;
      delete @rooms[name]
      @emit(&#x27;room_removed&#x27;, room)

    @emit(&#x27;room_created&#x27;, room)

    return room


  ###*
  # Create a new guest which might join the room with the given name
  # @method create_guest
  # @param {Channel} conn The connection to the guest
  # @param {String} room_name The name of the room to join
  # @return {Guest}
  ###
  create_guest: (conn, room_name) -&gt;
    return new Guest(conn, () =&gt; @get_room(room_name))


###*
# A room containing and conencting &#x60;Guest&#x60;s. Can be created by a &#x60;Hotel&#x60; or used alone.
# @class Room
# @extends events.EventEmitter
#
# @constructor
# @param {String} name
#
# @example
#     var room = new Room()
#     guest_a = room.create_guest(conn_a)
#     guest_b = room.create_guest(conn_b)
###
class Room extends EventEmitter

  ###*
  # A guest joined the room
  # @event guest_joined
  # @param {Guest} guest The new guest
  ###

  ###*
  # A guest left the room
  # @event guest_left
  # @param {Guest} guest The leaving guest
  ###

  ###*
  # The room was left by all guests
  # @event empty
  ###

  ###*
  # The name of the room
  # @property name
  # @readonly
  ###

  ###*
  # The current guests of the room
  # @property guests
  # @readonly
  # @private
  ###

  constructor: (@name) -&gt;
    @guests = {}


  ###*
  # Send a message to all guest except the sender
  # @method broadcast
  # @private
  # @param {Object} msg The message
  # @param {String} sender The id of the sender of the message (who will be skipped)
  ###
  broadcast: (msg, sender) -&gt;
    for id, guest of @guests
      if guest.id != sender
        guest.send(msg)


  ###*
  # Send a message to a guest
  # @method send
  # @private
  # @param {Object} msg The message
  # @param {String} recipient The recipient of the message
  # @return {Boolean} True if the recipient exists
  ###
  send: (msg, recipient) -&gt;
    if @guests[recipient]
      @guests[recipient].send(msg)
      return true
    else
      return false


  ###*
  # A guest joins the room. Will be removed when it emits &#x27;left&#x27;
  # @method join
  # @private
  # @param {Guest} guets The guest which joins the room
  # @return {Boolean} &#x60;true&#x60; if and only if the guest could join
  ###
  join: (guest) -&gt;
    if @guests[guest.id]?
      return false

    @guests[guest.id] = guest

    @emit(&#x27;guest_joined&#x27;, guest)

    guest.on &#x27;left&#x27;, () =&gt;
      if not @guests[guest.id]?
        return

      delete @guests[guest.id]

      @emit(&#x27;guest_left&#x27;, guest)

      if is_empty(@guests)
        @emit(&#x27;empty&#x27;)

    return true


  ###*
  # Create a guest which might join the room
  # @method create_guest
  # @param {Channel} conn The connection to the guest
  # @return {Guest}
  ###
  create_guest: (conn) -&gt;
    return new Guest(conn, () =&gt; @)


###*
# A guest which might join a &#x60;Room&#x60;.
#
# It will join the room once the client sends &#x27;join&#x27; and and leave once it emits the &#x27;left&#x27; event.
#
# @class Guest
# @extends events.EventEmitter
#
# @constructor
# @param {Channel} conn The connection to the guest
# @param {Function} room_fun Function which will be called upon joining and which should return the Room to join
###
class Guest extends EventEmitter

  ###*
  # Guest joined a room
  # @event joined
  # @param {Room} room The joined room
  ###

  ###*
  # Guest left the room
  # @event left
  # @param {Room} room The joined room
  ###

  ###*
  # The status of the guest changed
  # @event status_changed
  # @param {Object} status The new status
  ###

  ###*
  # The unique identifier of the guest
  # @property id
  # @readonly
  # @type String
  ###

  ###*
  # The status object of the guest. Will only be available after joining.
  # @property status
  # @readonly
  # @type Object
  ###

  constructor: (@conn, @room_fun) -&gt;
    @conn.on &#x27;message&#x27;, (data) =&gt; @receive(data)
    @conn.on &#x27;error&#x27;, (msg) =&gt; @error(msg)
    @conn.on &#x27;closed&#x27;, () =&gt; @closing()


  ###*
  # The guest receives data
  # @method receive
  # @private
  # @param {Object} data The incoming message
  ###
  receive: (data) -&gt;
    if not data.type?
      @error(&quot;Incoming message does not have a type&quot;)
      return

    switch data.type
      when &#x27;join&#x27;
        # get/create room

        @room = @room_fun()

        # get unique id

        while not @id? or @room.guests[@id]?
          @id = uuid.v4()

        # prepare peer list

        peers = {}

        for id, guest of @room.guests
          peers[id] = guest.status

        # try to join

        if not @room.join(@)
          @error(&quot;Unable to join&quot;)
          return

        # save status

        @status = data.status or {}

        # tell new guest

        @send({
          type: &#x27;joined&#x27;
          id: @id
          peers: peers
        })

        # tell everyone else

        @room.broadcast({
          type: &#x27;peer_joined&#x27;
          peer: @id
          status: @status
        }, @id)

        # tell library user

        @emit(&#x27;joined&#x27;, @room)
        @emit(&#x27;status_changed&#x27;, @status)

      when &#x27;to&#x27;
        if not data.peer? or not data.event?
          @error(&quot;&#x27;to&#x27; is missing a mandatory value&quot;)
          return

        if not @room?
          @error(&quot;Attempted &#x27;to&#x27; without being in a room&quot;)
          return

        # pass on
        if not @room.send({type: &#x27;from&#x27;, peer: @id, event: data.event, data: data.data}, data.peer)
          @error(&quot;Trying to send to unknown peer&quot;)

      when &#x27;status&#x27;
        if not data.status?
          @error(&quot;&#x27;update_status&#x27; is missing the status&quot;)
          return

        if not @room?
          @error(&quot;Attempted &#x27;status&#x27; without being in a room&quot;)
          return

        @status = data.status
        @emit(&#x27;status_changed&#x27;, @status)

        @room.broadcast({type: &#x27;peer_status&#x27;, peer: @id, status: data.status}, @id)

      when &#x27;leave&#x27;
        @conn.close()


  ###*
  # The guest sends data
  # @method send
  # @private
  # @param {Object} data The outgoing message
  ###
  send: (data) -&gt;
    @conn.send(data)


  ###*
  # The guest encountered an error
  # @method error
  # @private
  # @param {Error} The error which was encountered
  ###
  error: (msg) -&gt;
    # tell client
    @send {
      type: &#x27;error&#x27;
      msg: msg
    }

    # tell library user
    #@emit(&#x27;error&#x27;, msg)


  ###*
  # The connection to the guest is closing
  # @method closing
  # @private
  ###
  closing: () -&gt;
    @room?.broadcast {
      type: &#x27;peer_left&#x27;
      peer: @id
    }, @id

    @emit(&#x27;left&#x27;)


module.exports =
  Hotel: Hotel
  Room: Room
  Guest: Guest


    </pre>
</div>
                    </div>
                </div>
            </div>
        </div>
    </div>
</div>
<script src="../assets/vendor/prettify/prettify-min.js"></script>
<script>prettyPrint();</script>
<script src="../assets/js/yui-prettify.js"></script>
<script src="../assets/../api.js"></script>
<script src="../assets/js/api-filter.js"></script>
<script src="../assets/js/api-list.js"></script>
<script src="../assets/js/api-search.js"></script>
<script src="../assets/js/apidocs.js"></script>
</body>
</html>
